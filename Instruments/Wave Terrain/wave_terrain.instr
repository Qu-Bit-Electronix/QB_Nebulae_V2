; Wave Terrain
;
; Ported from Nebulae_V1 by:
; Chris Erin
; github.com/chriserin
; chris.erin@gmail.com

instr 1

  giwaveterrain ftgen 0, 0, 8192, 10, 1, 0, 0.33, 0, 0.2, 0, 0.14, 0, 0.11 ; Wavetable for wave terrain
  gisinewave    ftgen 0, 0, 4096, 10, 1                                    ; Sine wave for LFOs

  kndx1    scale gkspeed, 0.5, 0     ; LFO Index 1 (Speed)
  kpitch   scale gkpitch, 11, 6      ; Pitch
  kpitch   port kpitch, .01
  kpitch   pow  2, kpitch           ; Make pitch 1V/Octave
  kndx2    scale gkblend, 0.5, 0     ; LFO Index 2 (Mix)
  kcutoff  scale gkoverlap, 6000, 20 ; Cutoff Frequency (Grain Size)
  kres     scale gkdensity, 0.8, 0   ; Resonance (Grain Rate)
  krate1   scale gkloopstart, 1, 0.1 ; LFO 1 Rate (Loop Start)
  krate2   scale gkloopsize,  1, 0.1 ; LFO 2 Rate (Loop Size)

  kcx poscil kndx1, krate1, gisinewave                  ; High precision LFO
  kcx = kcx + 0.5                                      ; Converts waveform from bipolar to unipolar positive
  krx poscil kndx2, krate2, gisinewave                  ; High precision LFO
  krx = krx + 0.5                                      ; Converts waveform from bipolar to unipolar positive

  asig wterrain 0.8, kpitch, kcx, kcx, -krx, krx, giwaveterrain, giwaveterrain ; Wave terrain oscillator
  afilt moogvcf2 asig, kcutoff, kres                   ; Moog-style ladder filter
  out afilt                                            ; Mono output

endin
